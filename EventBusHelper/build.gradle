plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

group = 'com.github.dsoft2010'
version = '1.0.0'

android {
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'kr.ds.helper'
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'

    // for lifecycleScope
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Rxjava3
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = "$group"
                artifactId = 'eventbus-helper'
                version = "$version"
            }
        }
        
        repositories {
            maven {
                name = "GitHubPackages"
                
                // Replace GITHUB_USERID with your personal or organisation user ID and 
                // REPOSITORY with the name of the repository on GitHub
                url = uri("https://maven.pkg.github.com/dsoft2010/EventBusHelper")

                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("TOKEN")
                }
            }
        }
    }
}
